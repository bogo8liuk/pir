// If you add new keywords (excluding embracers like parenthesys and stuff like
// them), please make sure to update this list
reserved_keyword = _{
	  loop_keyword
	| channel_declaration_keyword
	| send_keyword
	| receive_keyword
	| proc_then
}

program = _{
	  SOI ~ process ~ EOI
}

process = !{ process_enclosing }

// The parenthesys enclosing cannot be placed only into `process_primary` rule
// because statements with infix operators (for processes) would be cut out
// otherwise
process_enclosing = _{
	  process_sequence
	| enclosing_start_keyword ~ process_enclosing ~ enclosing_end_keyword
}

process_sequence = _{ process_primary ~ (proc_infix ~ process_primary)* }

process_primary = _{
	  loop
	| send
	| expression
	| channel_declaration
	| enclosing_start_keyword ~ process_primary ~ enclosing_end_keyword
}

loop = {
	  loop_keyword ~ loop_start_keyword ~ process ~ loop_end_keyword
}

loop_start_keyword = _{ enclosing_start_keyword }
loop_end_keyword = _{ enclosing_end_keyword }

loop_keyword = _{ "loop" }

// The `$` is used to avoid something like `letx` and to pretend at least one
// whitespace or comment between the declaration keyword and the id.
// Unfortunately, this implies putting non semantics rules by hand, which is
// normally done by pest with non-atomic rules. The at_least_one_non_semantics
// would be impossible to do with non-atomic rules because the single whitespace
// or comment must be put at the beginning and not at the end.
channel_declaration = ${ channel_declaration_keyword ~ at_least_one_non_semantics ~
	channel_identifier ~ non_semantics ~ proc_then ~ non_semantics ~ process
}

channel_declaration_keyword = _{ "chan" }

channel_scope_start_keyword = _{ enclosing_start_keyword }
channel_scope_end_keyword = _{ enclosing_end_keyword }

// Using `$` for the same reason of `channel_declaration` rule
send = ${ send_keyword ~ expression_after_send ~ non_semantics ~
	channel_identifier ~ non_semantics ~ proc_then ~ non_semantics ~ process }

send_keyword = _{ "send" }

// Any expression after send keyword must be enclosed with the exception for
// single-term expressions (is this the right terminology???), for instance
// literals and variables. This is quite ugly because we have to
// programmatically rehandle what we already handle in expressions (two sources
// of truth).
expression_after_send = _{
	  enclosed_expression_after_send
	| single_term_expression_after_send
}
enclosed_expression_after_send = ${ non_semantics ~ expr_start_keyword ~
	non_semantics ~ expression ~ non_semantics ~ expr_end_keyword }
// Single-term expressions have to be treated case by case, because some (but
// not all) of them requires at least one whitespace or comment.
single_term_expression_after_send = ${
	  (non_semantics ~ string_literal)
	| (non_semantics ~ char_literal)
	| (at_least_one_non_semantics ~ int_literal)
	| (at_least_one_non_semantics ~ float_literal)
	// TODO: variables
}

receive_keyword = _{ "receive" }

isolated_keyword = _{ reserved_keyword ~ at_least_one_non_semantics }
var_identifier = ${ !isolated_keyword ~ (head_var_identifier_char ~ tail_var_identifier_char*) }
channel_identifier = _{ var_identifier }

head_var_identifier_char = _{ ASCII_ALPHA_LOWER | "_" }
tail_var_identifier_char = _{ ASCII_ALPHA | "_" | ASCII_DIGIT }

expression = !{
	  int_expr
	| value
}

int_expr = {
	  num_prefix? ~ int_expr_primary ~ (num_infix ~ num_prefix? ~ int_expr_primary)*
}
int_expr_primary = _{ int_literal | expr_start_keyword ~ int_expr ~ expr_end_keyword }

expr_start_keyword = _{ enclosing_start_keyword }
expr_end_keyword = _{ enclosing_end_keyword }

value = {
	  literal
}

literal = {
      string_literal
    | char_literal
    | float_literal
    //| int_literal
}

string_literal = ${ str_lit_start_keyword ~ string_lit_char* ~ str_lit_end_keyword }
char_literal = ${ char_lit_start_keyword ~ char_lit_char ~ char_lit_end_keyword }
int_literal = { JOINED_ASCII_DIGITS }
float_literal = { JOINED_DOTTED_SEP_ASCII_DIGITS }

string_lit_char = {
      string_lit_not_escaped_char
    | string_lit_escaped_char
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

char_lit_char = {
      char_lit_not_escaped_char
    | char_lit_escaped_char
    // | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

string_lit_not_escaped_char = {
      !("\"" | "\\") ~ ANY
}
char_lit_not_escaped_char = {
      !("'" | "\\") ~ ANY
}
char_lit_escaped_char = {
	  "\\" ~ ("'" | "\\" | "b" | "n" | "r" | "t")
}
string_lit_escaped_char = {
	  "\\" ~ ("\"" | "\\" | "b" | "n" | "r" | "t")
}

str_lit_start_keyword = _{ "\"" }
str_lit_end_keyword = _{ "\"" }
char_lit_start_keyword = _{ "'" }
char_lit_end_keyword = _{ "'" }

num_infix = _{ add | sub | mul | div | mod }
num_prefix = _{ neg }
//postfix = _{}

neg = { "-" }
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
mod = { "%" }

proc_infix = _{ proc_or }

proc_or = { "|" }
proc_then = _{ "." }

enclosing_start_keyword = _{ "(" }
enclosing_end_keyword = _{ ")" }

WHITESPACE = _{ " " | "\t" | "\n" }
multiline_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
COMMENT = _{
      ("//" ~ (!"\n" ~ ANY)*)
    | multiline_comment
}
non_semantics = _{ WHITESPACE* ~ (COMMENT ~ WHITESPACE*)* }
at_least_one_non_semantics = _{ (WHITESPACE | COMMENT) ~ non_semantics }

// To avoid whitespaces between digits when parsing
JOINED_ASCII_DIGITS = @{ ASCII_DIGIT+ }
JOINED_DOTTED_SEP_ASCII_DIGITS = ${ JOINED_ASCII_DIGITS ~ "." ~ JOINED_ASCII_DIGITS }

